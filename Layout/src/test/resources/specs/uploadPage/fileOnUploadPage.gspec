@objects
    fileItem-*              //*[contains(@class,'StyledListFile')]/div/div
    iconNameFile-*          //*[@aria-hidden='false']//div[contains(@class,'fileHeaderstyle')]
    iconFile-*              //*[@aria-hidden='false']//div[contains(@class,'icon')]
    infoFileName-*          //*[@aria-hidden='false']//span[contains(@class,'file-info__name')]
    infoState-*             //*[@aria-hidden='false']//span[contains(@class,'file-info__state')]
    fileContent-*           //*[@aria-hidden='false']//div[contains(@class,'StyledContent')]
    selectLang-*            //*[@aria-hidden='false']//div[contains(@class,'select-language-pack')]
    transStatus-*           //*[@aria-hidden='false']//span/span[.='Transcribe']

# Check one file     
= File =
    @forEach [fileItem-*] as item, index as i
        ${item}:
            width 100% of dragger/width
            @on desktop,tablet
                height 60px
            @on mobile
                height 90px
        
        iconNameFile-${i}: 
            @on desktop,tablet
                inside ${item} ~15px left
        
        iconFile-${i}:
            inside iconNameFile-${i} 0px left
            height 32px
        
        infoFileName-${i},infoState-${i}:
            inside iconNameFile-${i}
        
        infoFileName-${i}:
            css font-size is "14px"
            css font-weight is "700"
            css color is "rgba(43, 48, 59, 1)"
            above infoState-${i}
        
        infoState-${i}:
            css font-size is "12px"
            css font-weight is "700"
            css color is "rgba(65, 117, 5, 1)"
        
        fileContent-${i}:
            @on mobile
                below iconNameFile-${i}
            @on desktop,tablet
                inside ${item} ~15px right

        selectLang-${i}:
            inside fileContent-${i} 0px left
            height 32px
            @on mobile
                width 172px
            @on desktop,tablet
                width 216px
                
        transStatus-${i}:
            inside fileContent-${i} 0px right
